#!/usr/bin/env arturo

getCorrectVersion: function [versionString][
    try? -> to :version versionString
    else [
        try? -> to :version strip replace versionString "v" ""
        else -> null
    ]
]

getRepoInfo: function [owner, repo, details][
    tgt: ~{tmp/|owner|-|repo|-|details\version|.zip}
    download.as: tgt details\url
    unzip "tmp" tgt
    delete tgt

    tgtFolder: first list "tmp"
    infoData: # "|tgtFolder|/info.art"

    delete.directory tgtFolder

    return infoData
]

getRepoDetails: function [owner,repo][
    try? [
        u: ~"https://api.github.com/repos/|owner|/|repo|/releases"
        data: read.json u

        availableVersions: new []
        loop data 'entry [
            if ver: <= getCorrectVersion entry\name [

                'availableVersions ++ #[
                    version: ver
                    timestamp: to :date entry\published_at
                    url: entry\zipball_url
                ]
            ]   
        ]
        latest: first availableVersions

        inspect getRepoInfo owner repo latest
        availableVersions
    ]
    else [
        null
    ]
]

if standalone? ::
    owner: arg\0
    repo: arg\1
    inspect getRepoDetails owner repo