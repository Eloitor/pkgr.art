#!/usr/bin/env arturo

AccessToken: read "github.pat"
IsCI?: key? env 'GITHUB_ACTION      ; if it's set, we're inside a GitHub workflow

;--------------------------------------------------
; Helper methods
;--------------------------------------------------

getGH: function [req][
    accepting: "application/vnd.github+json"
    apiVersion: "2022-11-28"

    (IsCI?)?[
        got: execute ~{!sh
            gh api -H "Accept: |accepting|" -H "X-GitHub-Api-Version: |apiVersion|" /|req|
        }
        return read.json got
    ][
        got: request.get.headers: #[
            "Accept": accepting
            "Authorization": ~"Bearer |AccessToken|"
            "X-GitHub-Api-Version": apiVersion
        ] ~{https://api.github.com/|req|} ø
        inspect got
        return read.json got\body
    ]
]

extractCorrectVersion: function [versionString][
    if throws? -> to :version versionString [
        if throws? -> to :version strip replace versionString "v" ""
            -> null
    ]
]

;--------------------------------------------------
; Main routines
;--------------------------------------------------

getReadme: function [owner, repo][
    got: getGH ~"repos/|owner|/|repo|/readme"
    if key? got 'download_url ->
        return read got\download_url
    return ø
]

getRepoInfo: function [owner, repo][
    if throws? [
        got: getGH ~"repos/|owner|/|repo|"
        licenseUrl: "--"
        if got\license [
            licenseInfo: read.json got\license\url
            licenseUrl: licenseInfo\html_url
        ]
        #[
            name: got\name
            fullName: got\full_name
            url: got\html_url
            description: got\description
            tags: got\topics
            license: #[
                name: (got\license)? -> got\license\spdx_id -> "--"
                url: licenseUrl
            ]
            readme: getReadme owner repo
            stats: #[
                stars: got\stargazers_count
                watch: got\watchers_count
            ]
            author: #[
                name: got\owner\login
                url: got\owner\html_url
                avatar: got\owner\avatar_url
            ]
        ]
    ] -> null
]

getReleaseInfo: function [owner, repo, version, url][
    if IsCI? [
        write.directory "tmp" ø
    ]

    tgt: ~{tmp/|owner|-|repo|-|version|.zip}
    download.as: tgt url
    unzip "tmp" tgt
    delete tgt

    tgtFolder: first list "tmp"
    infoFile: ~"|tgtFolder|/info.art"

    ret: #[]

    if exists? infoFile ->
        ret: # infoFile

    unless key? ret 'entry ->
        ret\entry: "main.art"

    unless ".art" = extract.extension ret\entry ->
        ret\entry: ret\entry ++ ".art"

    unless key? ret 'executable ->
        ret\executable: false
    
    if ret\executable [
        unless ".art" = extract.extension ret\executable ->
            ret\executable: ret\executable ++ ".art"
    ]

    unless key? ret 'depends ->
        ret\depends: []

    unless key? ret 'requires ->
        ret\requires: [> 0.0.0]

    unless exists? ~{|tgtFolder|/|ret\entry|} ->
        ret: null

    delete.directory tgtFolder

    return ret
]

getFolderSize: function [folder][
    subfiles: list folder
    if 0 = size subfiles -> return 0`B
    return fold.seed: 0`B subfiles [bef,filepath][
        (directory? filepath) ? -> bef + getFolderSize filepath
                                -> bef + volume filepath
    ]
]

getNoOfFiles: function [folder][
    subfiles: list folder
    if 0 = size subfiles -> return 0
    return fold.seed: 0 subfiles [bef,filepath][
        (directory? filepath) ? -> bef + getNoOfFiles filepath
                                -> bef + 1
    ]
]

getRepoDetails: function [owner, repo, downloadUrl][
    tmpName: ~{|owner|-|repo|}
    tmpZip: ~"|tmpName|.zip"
    tmpFolder: ~"|tmpName|"
    
    download.as: tmpZip downloadUrl
    unzip tmpFolder tmpZip

    repoSize: to :integer scalar getFolderSize tmpFolder
    repoFiles: getNoOfFiles tmpFolder

    delete.directory tmpFolder
    delete tmpZip

    return #[size: repoSize, files: repoFiles]
]

getVersions: function [owner, repo][
    greaterThan: (attr 'greaterThan) ?? 0.0.0
    available: new []
    try [
        releases: getGH ~"repos/|owner|/|repo|/releases"
        loop releases 'release [
            if ver: <= extractCorrectVersion release\tag_name [
                if ver > greaterThan [
                    if releaseInfo: <= getReleaseInfo owner repo ver release\zipball_url [
                        'available ++ #[
                            version: ver
                            date: to :date release\published_at
                            link: release\html_url 
                            details: getRepoDetails owner repo release\zipball_url
                            url: release\zipball_url
                            info: releaseInfo
                        ]
                    ]
                ]
            ]
        ]
    ]
    return available
]

;--------------------------------------------------
; Running directly from console
;--------------------------------------------------

if standalone? ::
    owner: arg\0
    repo: arg\1
    inspect getRepoInfo owner repo
    print as.pretty.code getVersions owner repo